pkgname=modem-exporter
pkgver="0.0.11"
pkgrel=1
pkgbranch=master
pkgdesc='modem-exporter'
arch=('x86_64' 'armv7h' 'aarch64')
url='https://github.com/DictumMortuum/modem-exporter'
license=(APACHE)
makedepends=(go awk)
source=("${pkgname}.tar.gz::${url}/archive/refs/heads/master.tar.gz")
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/$pkgname-$pkgbranch"
  awk '/version :=/ { print $3 }' main.go
}

build() {
  cd "$srcdir/$pkgname-$pkgbranch"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-s -ldflags=-w"
  export GOARCH="$ARCH"
  make build
}

package() {
  cd "$srcdir/$pkgname-$pkgbranch"
  install -Dm755 "modem-exporter" "$pkgdir/usr/local/bin/modem-exporter"
  install -Dm644 "assets/prometheus-modem-exporter.service" "$pkgdir/usr/lib/systemd/system/prometheus-modem-exporter.service"
  install -Dm644 "assets/modem_exporter.yaml" "$pkgdir/usr/share/modem_exporter/modem_exporter.yaml"
}
